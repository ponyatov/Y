%{
#include "hpp.hpp"

void incFile(sym* inc) {
	yyin = fopen(inc->value.c_str(),"r");
	if (!yyin) yyerror(inc->tagval());
	yypush_buffer_state(yy_create_buffer(yyin,YY_BUF_SIZE));
}
%}

%option noyywrap
%option yylineno

DF	[ \t]*[^ #\n]*
DR	[ \t]*[^#\n]*
S	[\+\-]?
D	[0-9]+

%x x
%%
\\.					T1(CHAR)
#[^\n]*\n			{W(yytext,false);}
<x>#[^\n]*\n		{W(yytext,false);BEGIN(INITIAL);}

^\.module{DF}		TX(Directive,MODULE)
^\.file{DF}			TX(Directive,FFILE)
^\.eof				TX(Directive,FEOF)
^\.inc{DF}			TX(Directive,INC)
^\.(title|about){DR}			TX(Directive,DIRECTIVE)
^\.end				{yyterminate();}

\@					{BEGIN(x);TC(AX);}
\$					{BEGIN(x);TC(SX);}
\?					{BEGIN(x);TC(QX);}
<x>\n				{BEGIN(INITIAL);}
<x>[ \t\r]+			W(yytext,false);
<x>[@$?]			{BEGIN(INITIAL);}

<x>{S}{D}			TX(Int,NUM)
<x>0x[0-9A-F]+		TO("hex",NUM)
<x>0b[01]+			TO("bin",NUM)
<x>{S}{D}\.{D}		TX(Num,NUM)
<x>{S}{D}[eE]{S}{D}	TX(Num,NUM)

<x>\+				TX(Op,ADD)
<x>\-				TX(Op,SUB)
<x>\*				TX(Op,MUL)
<x>\/				TX(Op,DIV)
<x>\^				TX(Op,POW)
<x>\<				TX(Op,LL)
<x>\>				TX(Op,GG)
<x>\:				TX(Op,COLON)

<x>\'[^\']*\'		TX(Str,STR)
<x>\"[^\"]*\"		TX(Str,STR)
<x>[a-zA-Z0-9_]+	TO("sym",SYM)
<x>\%[TFNE]			TO("sym",SYM)

<x>\[		TX(Op,LQ)
<x>\]		TX(Op,RQ)

\n			TC(CHAR)
.			TC(CHAR)

<<EOF>>	{ yypop_buffer_state(); if ( !YY_CURRENT_BUFFER ) yyterminate();}
%%
